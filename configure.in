# Configuration for ht://Dig 3.x
# Copyright (c) 1995-1998 Andrew Scherpbier
# Modifications Copyright (c) 1998-9 Geoffrey R. Hutchison
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2, or (at your option)
# any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
#

AC_INIT(htcommon/DocumentDB.cc)
VERSION=`cat ${srcdir}/.version`
AM_INIT_AUTOMAKE([htdig], $VERSION)
AC_PREREQ(2.13)

AC_CONFIG_HEADER(include/htconfig.h)
AC_PREFIX_DEFAULT(/opt/www/htdig)
HTDIG_TOP=`pwd`
AC_SUBST(HTDIG_TOP)

echo configuring ht://Dig version $VERSION

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CXX
AC_PROG_RANLIB
AC_PATH_PROG(AR, ar, ar)
AC_PATH_PROG(SHELL, sh, /bin/sh)
AC_PATH_PROG(SED, sed, /bin/sed)
AC_PATH_PROG(SORT, sort, /bin/sort)
AC_PATH_PROG(FIND, find, /bin/find)
AC_PATH_PROG(GUNZIP, gunzip, /bin/gunzip)
AC_CHECK_PROGS(TAR, tar gtar gnutar, tar)
AC_PATH_PROG(PDF_PARSER, acroread, /usr/local/bin/acroread)
AC_PATH_PROG(SENDMAIL, sendmail, /usr/lib/sendmail, 
   $PATH:/usr/libexec:/usr/sbin:/usr/lib:/usr/etc:etc)

dnl Check for AIX problems
AC_AIX

dnl Checks for libraries.
AC_CHECK_LIB(socket, socket)
AC_CHECK_LIB(nsl, t_accept)
AC_CHECK_LIB(z, deflate)

AC_SUBST(CXXFLAGS)dnl
AC_SUBST(LDFLAGS)dnl

dnl Checks for header files.
AC_HEADER_STDC
AC_HEADER_TIME

dnl More header checks--here use C++
AC_LANG_CPLUSPLUS
AC_CHECK_HEADERS(fcntl.h limits.h malloc.h sys/file.h sys/ioctl.h sys/time.h unistd.h getopt.h strings.h)
AC_CHECK_HEADER(fstream.h,nofstream=0,nofstream=1)
if test "x$nofstream" = "x1" ; then
AC_MSG_ERROR([To compile ht://Dig, you will need a C++ library. Try installing libg++.])
fi

dnl Checks for typedefs, structures, and compiler characteristics.
AC_LANG_C
AC_C_CONST
AC_STRUCT_TM
dnl AC_CHECK_SIZEOF(int *) => SIZEOF_INT_P

dnl Checks for library functions.
AC_CHECK_FUNCS(strdup strerror strstr)
AC_CHECK_FUNCS(localtime_r)
dnl Check for timegm
AC_CHECK_FUNC(timegm)
dnl AC_DEFINE(HAVE_TIMEGM),
dnl  AC_MSG_CHECKING(whether our timegm replacement works);
dnl  AC_TRY_RUN([
dnl #define TEST_TIMEGM 1
dnl #include "$srcdir/htlib/mktime.c"
dnl #include "$srcdir/htlib/timegm.c"
dnl ], AC_MSG_RESULT(yes), AC_MSG_ERROR([no!]), AC_MSG_RESULT([I hope so])))

AC_MSG_CHECKING(whether we need gethostname() prototype?)
AC_TRY_COMPILE([#include <unistd.h>
#include <stdio.h>
#include <sys/types.h>
#include <sys/socket.h>
#include <netinet/in.h>
#include <sys/ioctl.h>
#include <sys/uio.h>
#include <sys/file.h>
#include <fcntl.h>
#include <netdb.h>
#include <stdlib.h>
extern "C" int gethostname(char *, int);],
  [gethostname("sdsu.edu", (int) 8);],
  [AC_MSG_RESULT(no)],
  [AC_MSG_RESULT(yes);AC_DEFINE(NEED_PROTO_GETHOSTNAME)])

AC_LANG_CPLUSPLUS
AC_MSG_CHECKING(whether the third argument of getpeername can be a size_t?)
AC_TRY_COMPILE([#include <sys/types.h>
#include <sys/socket.h>],
[ int socket;
  struct sockaddr server;
  size_t length;
  getpeername(socket, &server, &length);],
  [AC_MSG_RESULT(yes);AC_DEFINE(GETPEERNAME_LENGTH_T, size_t)],
  [AC_MSG_RESULT(no);AC_DEFINE(GETPEERNAME_LENGTH_T, unsigned int)])
AC_LANG_C

AC_PATH_PROG(TSORT, tsort)
if test -z "$TSORT"; then
  AC_MSG_ERROR([GNU rx configuration needs tsort in path!])
fi

if test ! -d "./db"; then
  mkdir db
fi
AC_CONFIG_SUBDIRS(db/dist)
AC_CONFIG_SUBDIRS(rx)

AC_OUTPUT(CONFIG Makefile Makefile.config htcommon/Makefile htlib/Makefile htdig/Makefile htmerge/Makefile htnotify/Makefile htfuzzy/Makefile htsearch/Makefile makedp, chmod +x makedp, echo timestamp > stamp-h)

echo ""
echo ""
echo "Now you must run 'make' followed by 'make install'"
echo ""
