dnl Process this file with autoconf to produce a configure script.
AC_INIT(htlib/Connection.h)
AC_CONFIG_HEADER(include/htconfig.h)

dnl
dnl Figure out the version of HTDig
dnl
export srcdir
HTDIG_VERSION=`cat $srcdir/.version`

echo configuring HTDig version $HTDIG_VERSION
AC_SUBST(HTDIG_VERSION)
AC_DEFINE_UNQUOTED(HTDIG_VERSION, "$HTDIG_VERSION")

AC_PROG_CXX

if test "x$CXX" = "xgcc" -o -z "$CXX" ; then
	echo "Unable to find a C++ compiler in the standard places."
	echo "Please set the CXX environment variable to the C++ compiler"
	echo "you want to use and run 'configure' again."
	exit 1
fi

AC_PROG_CC
AC_LANG_CPLUSPLUS

AC_SUBST(CXXFLAGS)dnl
AC_SUBST(LDFLAGS)dnl

dnl Checks for programs.
AC_PROG_RANLIB
AC_PATH_PROG(AR, ar, ar)
AC_PATH_PROG(SHELL, sh, /bin/sh)
AC_PATH_PROG(SED, sed, /bin/sed)
AC_PATH_PROG(SORT, sort, /bin/sort)
AC_CHECK_PROGS(TAR, tar gnutar, tar)

dnl Checks for libraries.
AC_CHECK_LIB(socket, socket, [LIBS="$LIBS -lsocket"])
AC_CHECK_LIB(nsl, t_accept, [LIBS="$LIBS -lnsl"])

dnl Checks for header files.
AC_CHECK_HEADERS(sys/time.h fcntl.h limits.h sys/file.h sys/ioctl.h unistd.h)
AC_HEADER_TIME

AC_MSG_CHECKING(for tm.tm_gmtoff)
AC_TRY_COMPILE([#include <time.h>], [struct tm t;t.tm_gmtoff = 0;],
  [AC_MSG_RESULT(yes);AC_DEFINE(HAVE_TM_GMTOFF)],
  [AC_MSG_RESULT(no)])

dnl Checks for typedefs, structures, and compiler characteristics.
AC_STRUCT_TM

dnl Checks for library functions.
AC_CHECK_FUNCS(mktime timelocal localtime gmtime)

HTDIG_TOP=`pwd`
AC_SUBST(HTDIG_TOP)

GDBM_DIR=`echo gdbm*`
AC_SUBST(GDBM_DIR)

RX_DIR=`echo rx*`
AC_SUBST(RX_DIR)

AC_MSG_CHECKING(need gethostname() prototype?)
AC_TRY_COMPILE([#include <unistd.h>
#include <stdio.h>
#include <sys/types.h>
#include <sys/socket.h>
#include <netinet/in.h>
#include <sys/ioctl.h>
#include <sys/uio.h>
#include <sys/file.h>
#include <fcntl.h>
#include <netdb.h>
#include <stdlib.h>
extern "C" int gethostname(char *, int);],
  [gethostname("sdsu.edu", (int) 8);],
  [AC_MSG_RESULT(no)],
  [AC_MSG_RESULT(yes);AC_DEFINE(NEED_PROTO_GETHOSTNAME)])

echo ""
echo "Configuring GNU GDBM"
(cd gdbm*; ./configure)
echo "GDBM Configuration complete"
echo ""
echo "Configuring GNU rx"
echo "Please ignore the warnings that may be displayed during this part"
(cd rx*; ./configure)
echo "GNU rx Configuration complete"
echo ""

AC_OUTPUT(makedp htlib/Makefile htmerge/Makefile htcommon/Makefile htdig/Makefile Makefile.config Makefile htsearch/Makefile htnotify/Makefile htfuzzy/Makefile, chmod +x makedp)

echo ""
echo ""
echo "Now you must run 'make' followed by 'make install'"
echo ""
