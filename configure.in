dnl Process this file with autoconf to produce a configure script.
AC_INIT(htcommon/DocumentDB.cc)
AM_INIT_AUTOMAKE(htdig, 3.1.0b2)
AC_CONFIG_HEADER(include/htconfig.h)
AC_PREFIX_DEFAULT(/opt/www/htdig)
HTDIG_TOP=`pwd`
AC_SUBST(HTDIG_TOP)

echo configuring ht://Dig version $VERSION

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CXX
AC_PROG_RANLIB
AC_PATH_PROG(AR, ar, ar)
AC_PATH_PROG(SHELL, sh, /bin/sh)
AC_PATH_PROG(SED, sed, /bin/sed)
AC_PATH_PROG(SORT, sort, /bin/sort)
AC_PATH_PROG(FIND, find, /bin/find)
AC_PATH_PROG(GUNZIP, gunzip, /bin/gunzip)
AC_CHECK_PROGS(TAR, tar gtar gnutar, tar)
AC_PATH_PROG(PDF_PARSER, acroread, /usr/local/bin/acroread)
AC_PATH_PROG(SENDMAIL, sendmail, /usr/lib/sendmail, 
   $PATH:/usr/libexec:/usr/sbin:/usr/lib:/usr/etc:etc)

dnl Check for AIX problems
AC_AIX

dnl Checks for libraries.
dnl Replace `main' with a function in -lcommon:
AC_CHECK_LIB(common, main)
dnl Replace `main' with a function in -ldb:
AC_CHECK_LIB(db, main)
dnl Replace `main' with a function in -lht:
AC_CHECK_LIB(ht, main)
dnl Replace `main' with a function in -lrx:
AC_CHECK_LIB(rx, main)
AC_CHECK_LIB(socket, socket, [LIBS="$LIBS -lsocket"])
AC_CHECK_LIB(nsl, t_accept, [LIBS="$LIBS -lnsl"])

AC_LANG_CPLUSPLUS

AC_SUBST(CXXFLAGS)dnl
AC_SUBST(LDFLAGS)dnl

dnl Checks for header files.
AC_HEADER_STDC
AC_HEADER_TIME
AC_CHECK_HEADERS(fcntl.h limits.h malloc.h sys/file.h sys/ioctl.h sys/time.h unistd.h)
AC_CHECK_HEADER(fstream.h,nofstream=0,nofstream=1)
if test "x$nofstream" = "x1" ; then
AC_MSG_ERROR([To compile ht://Dig, you will need a C++ library. Try installing libg++.])
fi

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_STRUCT_TM
dnl AC_CHECK_SIZEOF(int *) => SIZEOF_INT_P

dnl Checks for library functions.
AC_CHECK_FUNCS(strdup strerror strstr)
dnl Check for timegm
AC_CHECK_FUNC(timegm,
  AC_DEFINE(HAVE_TIMEGM),
  AC_MSG_CHECKING(whether our timegm replacement works);
  AC_TRY_RUN([
#define TEST_TIMEGM 1
#include "$srcdir/htlib/timegm.c"
], AC_MSG_RESULT(yes), AC_MSG_ERROR([no!]), AC_MSG_RESULT([I hope so])))

AC_MSG_CHECKING(whether we need gethostname() prototype?)
AC_TRY_COMPILE([#include <unistd.h>
#include <stdio.h>
#include <sys/types.h>
#include <sys/socket.h>
#include <netinet/in.h>
#include <sys/ioctl.h>
#include <sys/uio.h>
#include <sys/file.h>
#include <fcntl.h>
#include <netdb.h>
#include <stdlib.h>
extern "C" int gethostname(char *, int);],
  [gethostname("sdsu.edu", (int) 8);],
  [AC_MSG_RESULT(no)],
  [AC_MSG_RESULT(yes);AC_DEFINE(NEED_PROTO_GETHOSTNAME)])

echo ""
echo "Configuring Berkeley DBM"
DB_DIR=`echo db-*/build.unix`
if test ! -d $DB_DIR; then
  echo "Extracting files..."
  tarfile=`echo db-*.tar.gz`
  $GUNZIP < $tarfile | $TAR xf -
fi

DB_DIR=`echo db-*/build.unix`
if test ! -d $DB_DIR; then
  AC_MSG_ERROR([Please extract the Berkeley DB library])
fi
AC_SUBST(DB_DIR)

AC_CONFIG_SUBDIRS(db-*/dist)
dnl (cd db-*/build.unix; ../dist/configure)
echo "Berkeley DBM Configuration complete"
echo ""

AC_PATH_PROG(TSORT, tsort)
if test -z "$TSORT"; then
  AC_MSG_ERROR([GNU rx configuration needs tsort in path!])
fi

RX_DIR=`echo rx*`
AC_SUBST(RX_DIR)

echo "Configuring GNU rx"
echo "Please ignore the warnings that may be displayed during this part"
AC_CONFIG_SUBDIRS($RX_DIR)
echo "GNU rx Configuration complete"
echo ""

AC_OUTPUT(makedp htlib/Makefile htmerge/Makefile htcommon/Makefile htdig/Makefile Makefile.config Makefile CONFIG htsearch/Makefile htnotify/Makefile htfuzzy/Makefile, chmod +x makedp, echo timestamp > stamp-h)

echo ""
echo ""
echo "Now you must run 'make' followed by 'make install'"
echo ""
